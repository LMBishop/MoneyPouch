plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'com.leonardobishop'
version = '3.6'

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand('version': project.version)
    }
}

repositories {
    mavenCentral()

    // Spigot
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    // JitPack
    maven { url = uri('https://jitpack.io') }
    // bStats
    maven { url = uri('https://repo.codemc.org/repository/maven-public') }
    // AuthLib
    maven { url = 'https://libraries.minecraft.net/' }
}

dependencies {
    // bStats
    implementation 'org.bstats:bstats-bukkit-lite:1.8'
    // Spigot
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    // Vault
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    // LemonMobCoins
    compileOnly ('com.github.LemmoTresto:LemonMobCoins:master-SNAPSHOT') {
        exclude group: 'org.spongepowered'
        exclude group: 'co.aikar'
    }
    // AuthLib
    compileOnly 'com.mojang:authlib:1.5.21'
}

shadowJar {
    relocate 'org.bstats', 'com.leonardobishop.moneypouch.bstats'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
